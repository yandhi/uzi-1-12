buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:4.+"
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.0"
group = "me.kix.uzi"
archivesBaseName = "uzi"
def mcVersion = "1.12.2"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'fml.coreMods.load', 'me.kix.uzi.UziCoreMod'
        }
    }
}
repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        url = "https://repo.u-team.info"
    }

    mavenCentral()
    jcenter()
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"

    annotationProcessor ('org.spongepowered:mixin:0.8.3-SNAPSHOT:processor') {
        exclude module: 'gson'
    }
    implementation("org.spongepowered:mixin:0.8.5") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", mcVersion

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': mcVersion
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    defaultObfuscationEnv 'searge'
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'full'
}

build.dependsOn(shadowJar)

jar {
    exclude 'log4j2.xml'
    manifest {
        attributes(
                'FMLCorePlugin': 'me.kix.uzi.UziCoreMod',
                "TweakClass": 'org.spongepowered.asm.launch.MixinTweaker',
                "TweakOrder": 0,
                "MixinConfigs": 'mixins.uzi.json'
        )
    }
}
sourceCompatibility = targetCompatibility = "1.8"